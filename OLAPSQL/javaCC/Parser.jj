//set path =( $HOME/javacc2.1/bin $path)
options {

	IGNORE_CASE = true;
	/* pour la trace -->/*
	DEBUG_PARSER=false;
	DEBUG_LOOKAHEAD=false;
	DEBUG_TOKEN_MANAGER = false;
	/**/
	OPTIMIZE_TOKEN_MANAGER=true;
	BUILD_TOKEN_MANAGER = true;
}

PARSER_BEGIN(Analyzer)
package javaCC;
import structure.*;
import structure.types.*;
import structure.types.predicat.*;
import java.util.ArrayList;
	public class Analyzer{ 
		public Commande c;
	}
PARSER_END(Analyzer)


SKIP:
{" " | "\r" | "\n" | "\t"}


TOKEN:
{
		<CREATE		: "create">
   |	<DROP		: "drop">
   |	<ADD		: "add">
   |	<ALTER		: "alter">
   |	<INSERT		: "insert into">
   |	<DELETE		: "delete from">
   |	<VALUES		: "values">
   |	<SELECT		: "select">
 
   |	<FACT		: "fact">
   |	<LEVEL		: "level">
   |	<WITH		: "with">
   |	<HIERARCHY	: "hierarchy">
   |	<DIMENSION	: "dimension">
   |	<ROW		: "row">
   |	<COLUMN		: "column">
   
   |	<CONNECT	: "connect">
   |	<DISCONNECT	: "disconnect">
   |	<TO			: "to">
   |	<WHERE		: "where">
   |	<FROM		: "from">
   |	<AND		: "and">
   |	<OR			: "or">

//   |    <ENTREQUOT	: >
//symboles
    |	<OUVRANTE	: "(">
    |	<FERMANTE	: ")">
    |	<PTVIRG		: ";">
    |	<PT			: ".">
    |	<VIRG		: ",">
    |	<QUOT		: "'">
    |	<SLASH		: "/">       
//predicats	
/*	|	<EGAL		: "=">
	|	<NEGAL		: "<>">
	|	<SUP		: ">">
	|	<SUPEGAL	: ">=">
	|	<INF		: "<">
	|	<INFEGAL	: "<=">*/
	|	<OPERATOR	: ("="|"<>"|">"|">="|"<"|"<=")>
	|	<FONCT		: ("avg"|"count"|"sum"|"max"|"min")>
// types
   |	<DATE		: "date">
   |	<VARCHAR	: "varchar">
   |	<NUMBER		: "number">
   //doit commencer par une lettre
   |    <ENTIER		: (["0"-"9"])+>
   |    <CHIFFRE	: (["0"-"9"])+ 	((",")(["0"-"9"])+)?>				//(["0"-"9"]|",")*>
   |	<NOM		: (["A"-"Z"])+(["A"-"Z"]|["0"-"9"]|"_"|".")*>
}

/*----------------------------------
-	MAIN
-----------------------------------*/
ArrayList execute():{}{
	{Commande c=null; ArrayList l = new ArrayList();}
	
	(	c=create() 		{l.add(c);}
	|	c= drop()		{l.add(c);}
	|	c= alter()		{l.add(c);}
	|	c=insert()		{l.add(c);}
	|	c=delete()		{l.add(c);}
	|	c=select()		{l.add(c);}
	) *
	{ return l;}
}

Predicat executePredicat():{}{
	{return def_pred();}
}

ArrayList executeAggr():{}{
	{return def_aggr();}
}

/*----------------------------------
-	COMMANDES
-----------------------------------*/
	/*-------------
	-	CREATE -OK
	---------------*/
Create create():{}{
	{Create c;}
	<CREATE>(c=create_fact() | c=create_dim())
	{return c;}
}
CreateFact create_fact():
{}
{
	{ArrayList l, ll = new ArrayList(); Token t;}
	<FACT>t=<NOM>
	<OUVRANTE>
		l=def_attr()
		{CreateFact cf = new CreateFact(t.toString(), Commande.FACT,l);}
	<FERMANTE>
	<CONNECT><TO>t=<NOM> {ll.add(t.toString());}
				(<VIRG>t=<NOM>{ll.add(t.toString());})*
	<PTVIRG>
	{cf.addConnections(ll);return cf;}
}

CreateDimension create_dim():
{}
{
	{ArrayList l, lhs; Token t;Hierarchy h;}
	<DIMENSION>t=<NOM>
	<OUVRANTE>
		l=def_attr()
		{CreateDimension cd = new CreateDimension(t.toString(), Commande.DIMENSION,l);}
	<FERMANTE>
	(<WITH><HIERARCHY>t= <NOM> 
		<OUVRANTE>
			l = def_lev(){h= new Hierarchy (t.toString(), l);}
		<FERMANTE>
		{cd.ajoutHierarchy(h);}
	)*
	<PTVIRG>
	{return cd;}
}

	/*-------------
	-	DROP	-ok
	---------------*/
Drop drop():{}{
	{Drop d =null;}
	<DROP>( d=drop_fact()| d=drop_dim())
	{return d;}
}
Drop drop_fact():
{}
{
	{Token t;}
	<FACT>t=<NOM><PTVIRG>
	{return new Drop(t.toString(), Commande.FACT);}
}
Drop drop_dim():
{}
{	{Token t;}
	<DIMENSION>t=<NOM><PTVIRG>
	{return new Drop(t.toString(), Commande.DIMENSION);}
}

	/*-------------
	-	ALTER	-OK a ameliorer sur alter_fact
	---------------*/
Alter alter():{}{
	{Alter ca = null;}
	<ALTER>(ca=alter_fact()|ca=alter_dim())
	{return ca;}
}
Alter alter_fact():{}
{
	{Token t; ArrayList l;}
	<FACT>t= <NOM>{ Alter ca = new Alter (t.toString(), Commande.FACT);}
	(
		<ADD> l=def_attr(){ca.setAlteration(Alter.ADD);
							ca.setAttributs(l);}
							
	|	<DROP> t=<NOM> {ca.setAlteration(Alter.DROP);
						ca.ajoutString(t.toString());}
				(<VIRG>t=<NOM>{ca.ajoutString(t.toString());})*
				
	|	<CONNECT><TO> t=<NOM> {ca.setAlteration(Alter.CONNECT);
								ca.ajoutString(t.toString());}
				(<VIRG>t=<NOM>{ca.ajoutString(t.toString());})*
				
	|	<DISCONNECT><TO> t=<NOM> {ca.setAlteration(Alter.DISCONNECT);
									ca.ajoutString(t.toString());}
				(<VIRG>t=<NOM>{ca.ajoutString(t.toString());})*
	)?
	<PTVIRG>
	{return ca;}
}

Alter alter_dim():{}
{
	{Token t; ArrayList l; Alter ca=null;}
	<DIMENSION>t=<NOM>
	(
		<ADD> (  l=def_attr() {ca = new Alter(t.toString(), Commande.DIMENSION,Alter.ADD, l);}
			|	({ca = new AlterHierarchy (t.toString(), Commande.DIMENSION,AlterHierarchy.ADD_HIERARCHY);}
				<HIERARCHY> t= <NOM> 
					<OUVRANTE>
						l = def_lev(){Hierarchy h= new Hierarchy (t.toString(), l);}
					<FERMANTE>
				{((AlterHierarchy)ca).ajoutHierarchy(h);}
				))
	|	<DROP> ({ca = new Alter(t.toString(), Commande.DIMENSION,Alter.DROP);} 
				t=<NOM> {ca.ajoutString(t.toString());}
					(<VIRG>t=<NOM>{ca.ajoutString(t.toString());})*
			|	({ca = new AlterHierarchy (t.toString(), Commande.DIMENSION,AlterHierarchy.DROP_HIERARCHY);}
				<HIERARCHY> t= <NOM>
					{((AlterHierarchy)ca).ajoutHierarchy(new Hierarchy(t.toString(),null));}
				))
	)?
	<PTVIRG>
	{return ca;}
}	

	/*-------------
	-	INSERT	-OK
	---------------*/
Insert insert():{}{
	{Insert ci;}
	<INSERT>(ci=insert_fact()|ci=insert_dim())
	{return ci;}
}
InsertFact insert_fact():{}
{
	{Token t;String s;InsertFact cif=null;Predicat p;}
	<FACT> t=<NOM>
	<VALUES> <OUVRANTE>
			s=def_une_valeur() {cif = new InsertFact(t.toString(), Commande.FACT, s);}
			(<VIRG>s=def_une_valeur() {cif.ajoutValeur(s);})* 
		<FERMANTE>
	<CONNECT><TO>
		 t=<NOM> <WHERE> p=def_pred() {cif.ajoutConnect(t.toString(),p);}
		(<VIRG>t=<NOM> <WHERE> p=def_pred() {cif.ajoutConnect(t.toString(),p);})*
	<PTVIRG>
	{return cif;}
}
//ok
Insert insert_dim():{}
{
	{Token t;String s;}
	<DIMENSION>t=<NOM> {Insert ci = new Insert (t.toString(), Commande.DIMENSION);}
	<VALUES><OUVRANTE>
			s=def_une_valeur() {ci.ajoutValeur(s);}
				(<VIRG>s=def_une_valeur(){ci.ajoutValeur(s);})*  
			<FERMANTE>
	<PTVIRG>
	{return ci;}
}

	/*-------------
	-	DELETE -OK
	---------------*/
Delete delete():{}{
	{Delete d;}
	<DELETE>(d=del_fact()|d=del_dim())
	{return d;}
}
Delete del_fact():{}
{
	{Token t; Predicat p=null;}
	<FACT>t=<NOM>
	(<WHERE> p =def_pred())?
	<PTVIRG>
	{return new Delete(t.toString(), Commande.FACT,p);}
}
Delete del_dim():{}
{
	{Token t; Predicat p=null;}
	<DIMENSION>t=<NOM>
	(<WHERE> p=def_pred())?
	<PTVIRG>
	{return new Delete(t.toString(), Commande.DIMENSION,p);}
}

	/*-------------
	-	SELECT	-OK
	---------------*/
Select select():{}
{
	{Token t;ArrayList l;String s; Predicat p=null;}
	
	<SELECT> l=def_aggr(){Select cs = new Select (l);}
	<ROW> t=<NOM>	{cs.setNomRow(t.toString());}
		(<WITH> t= <NOM> {cs.setWithRow(t.toString());}
				(<OUVRANTE>
			t=<NOM>{s= t.toString();}(<PT>t=<NOM>{s= s+t.toString();})?
					{cs.ajoutRow(s);}
			(<VIRG>t=<NOM>{s= t.toString();}(<PT>t=<NOM>{s= s+t.toString();})?
					{cs.ajoutRow(s);})* 
				<FERMANTE>)?	
		)?
	<COLUMN> t=<NOM>	{cs.setNomColumn(t.toString());}
		(<WITH> t= <NOM> {cs.setWithColumn(t.toString());}
				(<OUVRANTE>
			t=<NOM>{s= t.toString();}(<PT>t=<NOM>{s= s+t.toString();})?
					{cs.ajoutColumn(s);}
			(<VIRG>t=<NOM>{s= t.toString();}(<PT>t=<NOM>{s= s+t.toString();})?
					{cs.ajoutColumn(s);})* 
				<FERMANTE>)?	
		)?		
	<FROM>t=<NOM> {cs.setNomFrom(t.toString());}
		(<WHERE> p=def_pred())?{cs.setWhere(p);}
	<PTVIRG>
	{return cs;}
}

/*----------------------------------
-	DEFINITIONS
-----------------------------------*/
//tdate renvoit une string, le prend comme token
String def_une_valeur():{}
{
		{Token t=null;String s="";}
		
		(<QUOT> (	s=tdate() 
				| 	(		(t=<NOM> {s = s+t.toString();})
						(t=<NOM> {s = s+" "+t.toString();})*
					)
				)								
		<QUOT>	{return s;})
		
	|	(t=<CHIFFRE>{return t.toString();})		
	|	(t=<ENTIER>{return t.toString();})
}

	/*-------------
	-	PREDICAT
	---------------*/
//pas de premiere parenthese gérée
//pas de or et and dans m parenthese
Predicat def_pred():{}
{
	{Predicat p=new Predicat ();; ElementAbstrait elmt;}
	
		elmt = def_elmt(null)
		
		//commun aux 2
	 	{	p.setRacine(elmt);
	 		return p;}
}

//pas encore ok
ElementAbstrait def_elmt(Liaison pere):{}{	
	{ElementAbstrait expr=null; Jointure j=null;
		Liaison l=null;}

	((<OUVRANTE>expr = def_elmt(pere)<FERMANTE>)

|	(
	 expr = def_jointure(pere)	
	(	(	<AND> {l = new Liaison (pere, Liaison.AND,expr);}
			(	(<OUVRANTE>expr = def_elmt(pere)<FERMANTE> {l.ajoutElmt(expr);
															expr.setPere(l);})
				|	(j = def_jointure(l)	{l.ajoutElmt(j);
										j.setPere(l);})
			)
			( <AND>	
				((<OUVRANTE>expr = def_elmt(pere)<FERMANTE> {l.ajoutElmt(expr);
															expr.setPere(l);})
			|	(j = def_jointure(l)	{l.ajoutElmt(j);
										j.setPere(l);}))
			)*
		
	 	{expr=l;})|
	 	(<OR> {l = new Liaison (pere, Liaison.OR,expr);}
		(	(<OUVRANTE>expr = def_elmt(pere)<FERMANTE> {l.ajoutElmt(expr);
															expr.setPere(l);})
			|	(j = def_jointure(l)	{l.ajoutElmt(j);
										j.setPere(l);}))
			( <OR>	
				((<OUVRANTE>expr = def_elmt(pere)<FERMANTE> {l.ajoutElmt(expr);
															expr.setPere(l);})
			|	(j = def_jointure(l)	{l.ajoutElmt(j);
										j.setPere(l);}))
			)*
		{expr=l;})
		 )?
	)
	)
	 {return expr;}
}

//ok
Jointure def_jointure(Liaison pere):{}{
	{Token t1, t2, t3; String s1="",s2="";}
	
	t1=<NOM> {s1=t1.toString();}(<PT>t1=<NOM>{s1=s1+"."+t1.toString();})? 
	t3=<OPERATOR>
			((t2=<NOM> {s2=t2.toString();}(<PT>t2=<NOM>{s2=s2+"."+t2.toString();})? 
				| (s2=def_une_valeur())))
	{return new Jointure(pere,s1,s2,t3.toString());}

}
	/*-------------
	-	END PREDICAT
	---------------*/

//AVG, SUM, MAX, COUNT, SOM
//renvoit une ArrayList de String 
//ok
ArrayList def_aggr():{}
{
	{Token tA, t; String s; ArrayList l = new ArrayList();}
	tA=<FONCT> <OUVRANTE>
			t=<NOM> {s = t.toString();}(<PT>t=<NOM>{s= s+t.toString();})?
			{	s = tA.toString()+"("+s+")";
				l.add(s);}
			<FERMANTE>
	(<VIRG>tA=<FONCT> <OUVRANTE>
			t=<NOM> {s = t.toString();}(<PT>t=<NOM>{s= s+t.toString();})?
			{	s = tA.toString()+"("+s+")";
				l.add(s);}
			<FERMANTE>)*
	{return l;}
}

/*renvoit une ArrayList d'attributs*/
//ok
ArrayList def_attr():{}
{
	{Token t; Attribut att;
	ArrayList l=new ArrayList();}
	
	t=<NOM> att=def_dom()
		{	att.setNom(t.toString());
			l.add(att);}
	 (<VIRG> t=<NOM> att=def_dom()
	 	{	att.setNom(t.toString());
			l.add(att);}
	 )*
	 {return l;}
}

/*renvoit un attribut typé mais sans nom*/
//ok
Attribut def_dom():{}
{
	{Attribut att=null;Token t;}
	(<DATE>{att = new Attribut(Attribut.DATE);}
	|	<NUMBER> <OUVRANTE>
				(t = <CHIFFRE> {String s = t.toString();int pos;
								if ((pos=s.indexOf(",")) !=0)
								{
									att = new Attribut (Attribut.NUMBER, Integer.parseInt(s.substring(0,pos)));
									att.setPrecision(Integer.parseInt(s.substring(pos+1)));
								}else{
									att = new Attribut (Attribut.NUMBER,Integer.parseInt(s));
								}
							}
				
				|t= <ENTIER> {att = new Attribut (Attribut.NUMBER, Integer.parseInt(t.toString()));}
				)
				<FERMANTE>
	|	<VARCHAR><OUVRANTE>
					t=<ENTIER>{att = new Attribut(Attribut.VARCHAR, Integer.parseInt(t.toString()));}
				<FERMANTE>)
	{return att;}
}

//ArrayList de levels
//ok
ArrayList def_lev():{}
{	
	{ArrayList l = new ArrayList(); Token t;Level level;}
	(
	<LEVEL> t=<NOM>{level = new Level(t.toString());}
		(<OUVRANTE>
			t=<NOM> {level.ajoutAttFaible(t.toString());}
			(<VIRG> t=<NOM>{level.ajoutAttFaible(t.toString());})*
		<FERMANTE>)?
		{l.add(level);}
	)+
	{return l;}
}

/*----------------------------------
-	TYPES 
-----------------------------------*/
//pour l'instant, retourne une string
String tdate ():{}
{
	{String dat=""; Token t;}
	t=<ENTIER>{dat= t.toString();}
	<SLASH>t=<ENTIER>{dat=dat+"-"+ t.toString();}
	<SLASH>t=<ENTIER>{dat=dat+"-"+ t.toString();}
	{return dat;}
}


